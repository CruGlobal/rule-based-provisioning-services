import org.ccci.idm.obj.SsoUser
import org.ccci.idm.obj.RoleAssignment
import org.ccci.idm.rules.obj.EmployeeInfo
import java.util.Date
import org.ccci.idm.obj.IdentityUser

global java.util.Date now

function Date addDaysToDates(Date termDate, int graceDays)
{
    if(termDate==null) return null;
    Date termDatePlusGrace = new Date();
    termDatePlusGrace.setTime(termDate.getTime()+graceDays*24L*60*60*1000);
    return termDatePlusGrace;
}

function boolean isActiveOrLeaveOrPartTime(String status)
{
	return ("A".equals(status) || "L".equals(status) || "P".equals(status));
}

declare ActiveGracePeriod
	endDate : Date
end

rule "ActivateGracePeriodForStaff"
	when
		EmployeeInfo( !isActiveOrLeaveOrPartTime(emplStatus),
					  paygroup in ("USS", "INT", "RCE", "SAL", "HFT", "HPT"),
					  $endDate : addDaysToDates(termDateAsDate, 90)>=now)
	then
		insert(new ActiveGracePeriod($endDate));
end

rule "ActivateGracePeriodForInterns"
	when
		EmployeeInfo( !isActiveOrLeaveOrPartTime(emplStatus),
					  paygroup not in ("USS", "INT", "RCE", "SAL", "HFT", "HPT"),
					  $endDate : addDaysToDates(termDateAsDate, 30)>=now)
	then
		insert(new ActiveGracePeriod($endDate));
end

rule "EnableStaffWebConsumerWhenActive"
    when
        EmployeeInfo( isActiveOrLeaveOrPartTime(emplStatus) )
    then
        insert(new RoleAssignment("roles:StaffOnlyConsumer"));
end

rule "WithinGracePeriod"
    when
        EmployeeInfo( !isActiveOrLeaveOrPartTime(emplStatus) )
        ActiveGracePeriod($endDate : endDate)
    then
        insert(new RoleAssignment("roles:StaffOnlyConsumer", $endDate));
end

rule "EnableStaffWebConsumerForNationalStaff"
	when
		not ( EmployeeInfo() )
		IdentityUser(designation.valid == true)
	then
		insert(new RoleAssignment("roles:StaffOnlyConsumer"));
end

/*
		IdentityUser(designation.valid == true)
*/
/*
This would remove manaully-asserted roles... so that wouldn't be good
rule "RemoveManuallyAssignedStaffWebConsumer"
    when
        EmployeeInfo( !isActiveOrLeaveOrPartTime(emplStatus) )
        not ActiveGracePeriod()
        r : RoleAssignment( roleId=="roles:StaffOnlyConsumer" )
    then
        retract( r );
end
*/

