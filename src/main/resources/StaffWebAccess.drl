import org.ccci.idm.obj.SsoUser
import org.ccci.idm.obj.RoleAssignment
import org.ccci.idm.rules.obj.EmployeeInfo
import java.util.Date

global java.util.Date now

function Date addDaysToDates(Date termDate, int graceDays)
{
    Date termDatePlusGrace = new Date();
    termDatePlusGrace.setTime(termDate.getTime()+graceDays*24L*60*60*1000);
    return termDatePlusGrace;
}

declare ActiveGracePeriod
	endDate : Date
end

rule "ActivateGracePeriodForStaff"
	when
		EmployeeInfo( emplStatus != "A",
					  paygroup in ("USS", "INT", "RCE", "SAL", "HFT", "HPT"),
					  $endDate : addDaysToDates(termDateAsDate, 90)>=now)
	then
		insert(new ActiveGracePeriod($endDate));
end

rule "ActivateGracePeriodForInterns"
	when
		EmployeeInfo( emplStatus != "A",
					  paygroup not in ("USS", "INT", "RCE", "SAL", "HFT", "HPT"),
					  $endDate : addDaysToDates(termDateAsDate, 30)>=now)
	then
		insert(new ActiveGracePeriod($endDate));
end

rule "EnableStaffWebConsumerWhenActive"
    when
        EmployeeInfo( emplStatus == "A" )
    then
        insert(new RoleAssignment("ccci:itroles:uscore:stellent:roles:StaffOnlyConsumer"));
end

rule "WithinGracePeriod"
    when
        EmployeeInfo( emplStatus != "A" )
        ActiveGracePeriod($endDate : endDate)
    then
        insert(new RoleAssignment("ccci:itroles:uscore:stellent:roles:StaffOnlyConsumer", $endDate));
end

/*
This would remove manaully-asserted roles... so that wouldn't be good
rule "RemoveManuallyAssignedStaffWebConsumer"
    when
        EmployeeInfo( emplStatus != "A" )
        not ActiveGracePeriod()
        r : RoleAssignment( roleId=="ccci:itroles:uscore:stellent:roles:StaffOnlyConsumer" )
    then
        retract( r );
end
*/

